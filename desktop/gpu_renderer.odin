package main

import "core:bytes"
import "core:c"
import "core:fmt"
import "core:image"
import "core:image/jpeg"
import "core:image/png"
import "core:log"
import "core:mem"
import "core:os"
import filepath "core:path/filepath"
import strings "core:strings"
import sdl "vendor:sdl3"

Pipeline_Kind :: enum {
	Frame,
}

GPU_Renderer :: struct {
	window:                 ^sdl.Window,
	pixel_width:            u32,
	pixel_height:           u32,
	device:                 ^sdl.GPUDevice,
	pipelines:              [Pipeline_Kind]^sdl.GPUGraphicsPipeline,
	depth_texture:          ^sdl.GPUTexture,
	fallback_texture:       Texture, // used when materials dont have a texture
	nearest_repeat_sampler: ^sdl.GPUSampler, // pixel art, checkerboard, procedural textures
	nearest_clamp_sampler:  ^sdl.GPUSampler, // sprite sheets (no edge bleed)
	linear_repeat_sampler:  ^sdl.GPUSampler, // 3D model textures
	swapchain_format:       sdl.GPUTextureFormat,
}

Texture :: struct {
	sdl_texture: ^sdl.GPUTexture,
	width:       u32,
	height:      u32,
}

Image_Type :: enum {
	PNG,
	JPEG,
}

init_renderer :: proc() {
	// Create window
	renderer := &platform.gpu_renderer
	app_name := strings.unsafe_string_to_cstring(WindowAppName)
	renderer.window = sdl.CreateWindow(app_name, WindowWidth, WindowHeight, {.RESIZABLE})
	if renderer.window == nil {
		log_sdl_fatal("Failed to create window")
	}

	// Init ShaderCross — runtime SPIR-V transpilation to native GPU format
	if !ShaderCross_Init() {
		log.fatalf("Failed to init ShaderCross: %s", sdl.GetError())
		panic("ShaderCross init failed")
	}

	shader_formats := ShaderCross_GetSPIRVShaderFormats()
	log.infof("ShaderCross supported formats: %v", shader_formats)

	renderer.device = sdl.CreateGPUDevice(shader_formats, ODIN_DEBUG, nil)
	if renderer.device == nil {
		log_sdl_fatal("Failed to create GPU device")
	}

	// Claim window for GPU rendering
	if !sdl.ClaimWindowForGPUDevice(renderer.device, renderer.window) {
		log_sdl_fatal("Failed to claim window")
	}
	// Get actual pixel dimensions (may differ from logical on HiDPI/Retina)
	pixel_w, pixel_h: c.int
	assert(sdl.GetWindowSizeInPixels(renderer.window, &pixel_w, &pixel_h))
	log.infof("Window: %dx%d logical, %dx%d pixels", WindowWidth, WindowHeight, pixel_w, pixel_h)

	renderer.pixel_height = u32(pixel_h)
	renderer.pixel_width = u32(pixel_w)

	// VSync on by default
	// todo when we are saving settings, make sure to make this load that state
	assert(sdl.SetGPUSwapchainParameters(renderer.device, renderer.window, .SDR, .VSYNC))

	renderer.swapchain_format = sdl.GetGPUSwapchainTextureFormat(renderer.device, renderer.window)
	log.infof("using swapchain format: %v", renderer.swapchain_format)

	// fallback white texture
	renderer.fallback_texture = load_texture_from_pixels(1, 1, []byte{255, 255, 255, 255})

	// Depth buffer
	renderer.depth_texture = sdl.CreateGPUTexture(
		renderer.device,
		sdl.GPUTextureCreateInfo {
			type = .D2,
			format = .D32_FLOAT,
			width = u32(renderer.pixel_width),
			height = u32(renderer.pixel_height),
			layer_count_or_depth = 1,
			num_levels = 1,
			usage = {.DEPTH_STENCIL_TARGET},
		},
	)
	if renderer.depth_texture == nil {
		log_sdl_fatal("Failed to create depth texture")
	}

	renderer.nearest_repeat_sampler = sdl.CreateGPUSampler(
		renderer.device,
		sdl.GPUSamplerCreateInfo {
			min_filter = .NEAREST,
			mag_filter = .NEAREST,
			address_mode_u = .REPEAT,
			address_mode_v = .REPEAT,
		},
	)
	if renderer.nearest_repeat_sampler == nil {
		log_sdl_fatal("Failed to create sampler")
	}

	renderer.nearest_clamp_sampler = sdl.CreateGPUSampler(
		renderer.device,
		sdl.GPUSamplerCreateInfo {
			min_filter = .NEAREST,
			mag_filter = .NEAREST,
			address_mode_u = .CLAMP_TO_EDGE,
			address_mode_v = .CLAMP_TO_EDGE,
		},
	)
	if renderer.nearest_clamp_sampler == nil {
		log_sdl_fatal("Failed to create sprite sampler")
	}

	// Pipelines

	// Load shaders (into scratch — bytes only needed until ShaderCross compiles them)
	shader_count: int
	shader_start := sdl.GetPerformanceCounter()

	// Sprite pipeline
	sprite_vert_shader := load_shader("build/shaders/frame.vert.spv", .VERTEX, 1, 0)
	defer sdl.ReleaseGPUShader(renderer.device, sprite_vert_shader)
	shader_count += 1
	sprite_frag_shader := load_shader("build/shaders/frame.frag.spv", .FRAGMENT, 0, 1)
	defer sdl.ReleaseGPUShader(renderer.device, sprite_frag_shader)
	shader_count += 1

	renderer.pipelines[.Frame] = create_frame_pipeline(sprite_vert_shader, sprite_frag_shader)

	// Report shader compilation time
	shader_elapsed := elapsed_ms(shader_start)
	log.infof("Shader compilation: %.2fms (%d shaders)", shader_elapsed, shader_count)
}

deinit_renderer :: proc() {
	// destroy pipelines
	for pipeline in platform.gpu_renderer.pipelines {
		sdl.ReleaseGPUGraphicsPipeline(platform.gpu_renderer.device, pipeline)
	}

	sdl.ReleaseGPUSampler(platform.gpu_renderer.device, platform.gpu_renderer.linear_repeat_sampler)
	sdl.ReleaseGPUSampler(platform.gpu_renderer.device, platform.gpu_renderer.nearest_clamp_sampler)
	sdl.ReleaseGPUSampler(platform.gpu_renderer.device, platform.gpu_renderer.nearest_repeat_sampler)
	sdl.ReleaseGPUTexture(platform.gpu_renderer.device, platform.gpu_renderer.depth_texture)
	unload_texture(platform.gpu_renderer.fallback_texture)
	sdl.DestroyGPUDevice(platform.gpu_renderer.device)
	sdl.DestroyWindow(platform.gpu_renderer.window)
	ShaderCross_Quit()
}

renderer_resize_viewport :: proc(width, height: u32) {
	sdl.ReleaseGPUTexture(platform.gpu_renderer.device, platform.gpu_renderer.depth_texture)
	platform.gpu_renderer.depth_texture = sdl.CreateGPUTexture(
		platform.gpu_renderer.device,
		sdl.GPUTextureCreateInfo {
			type = .D2,
			format = .D32_FLOAT,
			width = width,
			height = height,
			layer_count_or_depth = 1,
			num_levels = 1,
			usage = {.DEPTH_STENCIL_TARGET},
		},
	)
	platform.gpu_renderer.pixel_height = height
	platform.gpu_renderer.pixel_width = width
	if platform.gpu_renderer.depth_texture == nil {
		log_sdl_fatal("Failed to recreate depth texture on resize")
	}
}

renderer_enable_vsync :: proc(enable: bool) {
	if !sdl.SetGPUSwapchainParameters(
		platform.gpu_renderer.device,
		platform.gpu_renderer.window,
		.SDR,
		enable ? .VSYNC : .IMMEDIATE,
	) {
		state := enable ? "enable" : "disable"
		log_sdl_warn(fmt.tprintf("failed to %s vsync", state))
	}
}

renderer_begin_frame :: proc() -> (^sdl.GPUCommandBuffer, ^sdl.GPURenderPass, bool) {
	// Acquire command buffer
	cmd := sdl.AcquireGPUCommandBuffer(platform.gpu_renderer.device)
	if cmd == nil {
		log_sdl_error("Failed to acquire GPU command buffer")
		return nil, nil, false
	}

	// Acquire swapchain texture
	swapchain_tex: ^sdl.GPUTexture
	if !sdl.WaitAndAcquireGPUSwapchainTexture(cmd, platform.gpu_renderer.window, &swapchain_tex, nil, nil) {
		log_sdl_error("Failed to acquire GPU swapchain texture")
		_ = sdl.SubmitGPUCommandBuffer(cmd)
		return nil, nil, false
	}
	if swapchain_tex == nil {
		// Window minimized or not visible — submit empty command buffer
		_ = sdl.SubmitGPUCommandBuffer(cmd)
		return nil, nil, false
	}

	// Begin render pass — clear to dark gray, clear depth to 1.0
	color_target := sdl.GPUColorTargetInfo {
		texture     = swapchain_tex,
		load_op     = .CLEAR,
		store_op    = .STORE,
		clear_color = {0.1, 0.1, 0.1, 1.0},
	}
	depth_target := sdl.GPUDepthStencilTargetInfo {
		texture     = platform.gpu_renderer.depth_texture,
		load_op     = .CLEAR,
		store_op    = .STORE,
		clear_depth = 1.0,
	}
	render_pass := sdl.BeginGPURenderPass(cmd, &color_target, 1, &depth_target)
	return cmd, render_pass, true
}

renderer_end_frame :: proc(cmd: ^sdl.GPUCommandBuffer, render_pass: ^sdl.GPURenderPass) {
	sdl.EndGPURenderPass(render_pass)

	// Submit
	if !sdl.SubmitGPUCommandBuffer(cmd) {
		log_sdl_error("Failed to submit GPU command buffer")
	}
}

load_shader :: proc(
	spv_path: string,
	stage: sdl.GPUShaderStage,
	num_uniform_buffers: u32,
	num_samplers: u32,
	allocator: mem.Allocator = context.temp_allocator,
) -> ^sdl.GPUShader {
	code, read_err := os.read_entire_file(spv_path, allocator)
	if read_err != nil {
		log.fatalf("Failed to load shader: %s", spv_path)
		panic("shader load failed")
	}

	sc_stage: ShaderCross_ShaderStage = stage == .VERTEX ? .VERTEX : .FRAGMENT

	shader := ShaderCross_CompileGraphicsShaderFromSPIRV(
		platform.gpu_renderer.device,
		&ShaderCross_SPIRV_Info {
			bytecode = raw_data(code),
			bytecode_size = c.size_t(len(code)),
			entrypoint = "main",
			shader_stage = sc_stage,
		},
		&ShaderCross_GraphicsShaderResourceInfo {
			num_samplers = num_samplers,
			num_uniform_buffers = num_uniform_buffers,
		},
		0,
	)

	if shader == nil {
		log.fatalf("Failed to compile shader: %s: %s", spv_path, sdl.GetError())
		panic("shader compilation failed")
	}
	return shader
}

load_texture :: proc {
	load_texture_from_file,
	load_texture_from_pixels,
	load_texture_from_memory,
}

load_texture_from_file :: proc(path: string) -> Texture {
	file_ext := strings.to_lower(filepath.ext(path), context.temp_allocator)
	image_type: Image_Type
	if file_ext == ".png" {
		image_type = .PNG
	} else if file_ext == ".jpg" || file_ext == ".jpeg" {
		image_type = .JPEG
	} else {
		panic(fmt.tprintf("unknown file type for %s, must be 'png', 'jpeg', or 'jpg'", path))
	}

	buf, err := os.read_entire_file(path, context.temp_allocator)
	if err != nil {
		log.errorf("Failed to read file: %s", path)
		panic("texture file read failed")
	}
	return load_texture_from_memory(buf, image_type)
}

load_texture_from_memory :: proc(buf: []byte, image_type: Image_Type = .PNG) -> Texture {
	img: ^image.Image
	err: image.Error
	switch image_type {
	case .PNG:
		img, err = png.load_from_bytes(buf, {png.Options.alpha_add_if_missing}, context.temp_allocator)
	case .JPEG:
		img, err = jpeg.load_from_bytes(buf, {jpeg.Options.alpha_add_if_missing}, context.temp_allocator)
	}
	if err != nil {
		log.errorf("failed to decode %v: %v", image_type, err)
		panic("texture image load failed")
	}

	log.info("Loading texture from memory buffer")
	return load_texture_from_pixels(u32(img.width), u32(img.height), bytes.buffer_to_bytes(&img.pixels))
}

load_texture_from_pixels :: proc(tex_width, tex_height: u32, pixels_buf: []byte) -> Texture {
	sdl_texture := sdl.CreateGPUTexture(
		platform.gpu_renderer.device,
		sdl.GPUTextureCreateInfo {
			type = .D2,
			format = .R8G8B8A8_UNORM,
			width = tex_width,
			height = tex_height,
			layer_count_or_depth = 1,
			num_levels = 1,
			usage = {.SAMPLER},
		},
	)
	if sdl_texture == nil {
		log_sdl_fatal("Failed to create texture")
	}

	tex_transfer := sdl.CreateGPUTransferBuffer(
		platform.gpu_renderer.device,
		sdl.GPUTransferBufferCreateInfo{usage = .UPLOAD, size = u32(len(pixels_buf))},
	)
	transfer_buf_ptr := sdl.MapGPUTransferBuffer(platform.gpu_renderer.device, tex_transfer, false)
	mem.copy(transfer_buf_ptr, raw_data(pixels_buf), len(pixels_buf))
	sdl.UnmapGPUTransferBuffer(platform.gpu_renderer.device, tex_transfer)

	tex_upload_cmd := sdl.AcquireGPUCommandBuffer(platform.gpu_renderer.device)
	tex_copy_pass := sdl.BeginGPUCopyPass(tex_upload_cmd)
	sdl.UploadToGPUTexture(
		tex_copy_pass,
		sdl.GPUTextureTransferInfo {
			transfer_buffer = tex_transfer,
			pixels_per_row = u32(tex_width),
			rows_per_layer = u32(tex_height),
		},
		sdl.GPUTextureRegion{texture = sdl_texture, w = u32(tex_width), h = u32(tex_height), d = 1},
		false,
	)
	sdl.EndGPUCopyPass(tex_copy_pass)
	assert(sdl.SubmitGPUCommandBuffer(tex_upload_cmd), "failed to upload texture cmd buffer")
	sdl.ReleaseGPUTransferBuffer(platform.gpu_renderer.device, tex_transfer)

	log.infof("Loaded texture: %dx%d", tex_width, tex_height)
	return {sdl_texture = sdl_texture, height = tex_height, width = tex_width}
}

unload_texture :: proc(t: Texture) {
	sdl.ReleaseGPUTexture(platform.gpu_renderer.device, t.sdl_texture)
}

renderer_pipeline :: proc(kind: Pipeline_Kind) -> ^sdl.GPUGraphicsPipeline {
	return platform.gpu_renderer.pipelines[kind]
}

renderer_upload_buffer :: proc(data: []$T, usage: sdl.GPUBufferUsageFlag, name: string = "") -> ^sdl.GPUBuffer {
	data_size := u32(len(data) * size_of(T))
	ci := sdl.GPUBufferCreateInfo {
		usage = {usage},
		size  = data_size,
	}
	when ODIN_DEBUG {
		if name != "" {
			ci.props = sdl.CreateProperties()
			name_c := strings.clone_to_cstring(name, context.temp_allocator)
			sdl.SetStringProperty(ci.props, sdl.PROP_GPU_BUFFER_CREATE_NAME_STRING, name_c)
		}
	}
	buffer := sdl.CreateGPUBuffer(platform.gpu_renderer.device, ci)
	if buffer == nil {
		log_sdl_fatal("Failed to create GPU buffer")
	}

	when ODIN_DEBUG {
		if name != "" do sdl.DestroyProperties(ci.props)
	}
	// Upload to GPU with a copy pass
	transfer := sdl.CreateGPUTransferBuffer(
		platform.gpu_renderer.device,
		sdl.GPUTransferBufferCreateInfo{usage = .UPLOAD, size = data_size},
	)
	transfer_ptr := sdl.MapGPUTransferBuffer(platform.gpu_renderer.device, transfer, false)
	mem.copy(transfer_ptr, raw_data(data), int(data_size))
	sdl.UnmapGPUTransferBuffer(platform.gpu_renderer.device, transfer)

	upload_cmd := sdl.AcquireGPUCommandBuffer(platform.gpu_renderer.device)
	copy_pass := sdl.BeginGPUCopyPass(upload_cmd)
	sdl.UploadToGPUBuffer(
		copy_pass,
		sdl.GPUTransferBufferLocation{transfer_buffer = transfer, offset = 0},
		sdl.GPUBufferRegion{buffer = buffer, offset = 0, size = data_size},
		false,
	)
	sdl.EndGPUCopyPass(copy_pass)
	assert(sdl.SubmitGPUCommandBuffer(upload_cmd), "failed to submit GPU buffer upload command")
	sdl.ReleaseGPUTransferBuffer(platform.gpu_renderer.device, transfer)

	return buffer
}

renderer_release_vertex_buffer :: proc(buf: ^sdl.GPUBuffer) {
	sdl.ReleaseGPUBuffer(platform.gpu_renderer.device, buf)
}

@(private = "file")
create_frame_pipeline :: proc(vert_shader, frag_shader: ^sdl.GPUShader) -> ^sdl.GPUGraphicsPipeline {
	color_target_descs := [?]sdl.GPUColorTargetDescription{{format = platform.gpu_renderer.swapchain_format}}
	sprite_pipeline := sdl.CreateGPUGraphicsPipeline(
		platform.gpu_renderer.device,
		sdl.GPUGraphicsPipelineCreateInfo {
			vertex_shader = vert_shader,
			fragment_shader = frag_shader,
			primitive_type = .TRIANGLESTRIP,
			rasterizer_state = {fill_mode = .FILL, cull_mode = .NONE},
			depth_stencil_state = {compare_op = .LESS_OR_EQUAL, enable_depth_test = true, enable_depth_write = true},
			target_info = {
				color_target_descriptions = raw_data(&color_target_descs),
				num_color_targets = len(color_target_descs),
				depth_stencil_format = .D32_FLOAT,
				has_depth_stencil_target = true,
			},
		},
	)
	if sprite_pipeline == nil {
		log_sdl_fatal("Failed to create sprite pipeline")
	}

	return sprite_pipeline
}
